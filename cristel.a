          LST OFF
********************************
*                              *
*          CRISTEL             *
*                              *
* (C) Ch.QUEST   03/86         *
*                              *
********************************
*
*
*
PRODOS    = 1
BUFSEND   = 1
EXPO      = 0            ; CONNEXION SPECIALE
*
*
*
          DO PRODOS
          ORG $805
          ELSE
          ORG $8D00
          FIN
*
*
* ADRESSES ROM ET PAGE ZERO UTILISEES
*
SYNTERR   = $DEC9        ;AFFICHE SYNTAX ERROR
LINNUM    = $50
HIMEM     = $6F
FREE      = $73
FORPNT    = $85
CHRGET    = $B1          ;LECTURE D'UN CHAR DU PRGM
CHRGOT    = $B8          ;RELECTURE DERNIER CARAC
ONERR     = $D8
BUFFER    = $200
AMPER     = $3F5
HORL      = $C0C2        ;SLOT 4
          DO PRODOS
DOSCMD    = $BE03        ;EXECUTION COMMANDE DU PRODOS (BASIC)
DOSLEN    = $BEC8        ;L$ (BLOAD,BSAVE)
DOSADD    = $BE58        ;A$
CSW       = $BE30        ;ADRESSES DES ROUTINES
KSW       = $BE32        ;NORMAL D'AFFICHAGE (DOS)
          ELSE
DOSLEN    = $AA60
DOSADD    = $AA72
CSW       = $AA53
KSW       = $AA55
          FIN
COUT      = $FDED
BASERR    = $D412        ;GESTION DES ERREURS
NEWSTT    = $D7D2
GOTO      = $D941        ; GOTO EN (LINNUM)
SAVD      = $DA9A
CHKSTR    = $DD6C
CHKCOM    = $DEBE        ;ATTEND UNE VIRGULE
MAK       = $E3E9
WAITROM   = $FCA8        ;ROUTINE TEMPO (ROM)
PTRGET    = $DFE3        ;RECHERCHE VARIABLE
PRINORM   = $FDF0
NORINPUT  = $FD1B        ;ENTREE NORMALE D'UN CARACT
PRINTROM  = $DAD5        ;PRINT (ROM)
*
* SCRATCHPAD
*
SLOT16    = $6
SLOTCARD  = $7
TEMPOL    = $9
TEMPOH    = $18
TEMPO     = $19
TEMP      = $1A          ;STOCKAGE TEMPORAIRE
MINUS     = $1A
TEMP2     = $1B
XCOOR     = $1C
MINUS2    = $1C
YCOOR     = $1D
LEN       = $1E
CARINP    = $FE
LEN2      = $CE
MASK      = $FD
*
* TOKENS UTILISES
*
CLEAR:    = 189
COLOR:    = 160
DRAW:     = 148
END:      = 128
FLASH:    = 159
GET:      = 190
GR:       = 136
HCOLOR:   = 146
HOME:     = 151
POS:      = $D9
INPUT:    = 132
INVERSE:  = 158
NORMAL:   = 157
NOTRACE:  = $9C
PLOT:     = $8D
PRINT:    = 186
READ:     = 135
SCALE:    = 153
STEP:     = 199
TEXT:     = 137
TRACE:    = $9B
VTAB:     = 162
WAIT:     = 181
XDRAW:    = 149
LOAD:     = $B6
DEL:      = $85
IF:       = $AD
ON:       = $B4
AT:       = $C5
HTAB:     = $96
*
* CODES VIDEOTEX
*
BELL      = $7
BS        = $8
HT        = $9
LF        = $A
VT        = $B
FF        = $C
CR        = $D
SO        = $E
SI        = $F
CSRON     = $11
REP       = $12
SEP       = $13
CSROFF    = $14
SS2       = $16
CAN       = $18
SUB       = $1A
ESC       = $1B
RS        = $1E
US        = $1F
LINE      = $5A
NOLINE    = $59
ENVOI     = $41
RETOUR    = $42
REPETITI  = $43
GUIDE     = $44
ANNULATI  = $45
SOMMAIRE  = $46
CORRECTI  = $47
SUITE     = $48
CONFIN    = $49
*
* CONSTANTES
*
JOUR      = $26
*
* MISE EN PLACE...
*
INIT      LDA #DEBUT
          STA AMPER+1
          LDA #/DEBUT
          STA AMPER+2
          LDA #$4C
          STA AMPER
          LDA #32
          STA CARINP
          LDA #0
          STA XCOOR
          DO PRODOS
          LDA #ENDCRIS
          STA $67
          LDA #/ENDCRIS
          STA $68
          LDA #ENDCRIS+3
          STA $69
          STA $6B
          STA $6D
          LDA #/ENDCRIS+3
          STA $6A
          STA $6C
          STA $6E
          ELSE
          LDA #INIT
          STA FREE
          STA HIMEM
          LDA #/INIT
          STA FREE+1
          STA HIMEM+1
          FIN
          RTS
FILTER1   HEX 0E0F1B1E1F220D000000000000000000
FILTER2   HEX 00000000000000000000000000000000
CARSEND   HEX 00
CAREP     HEX 00
NBREP     HEX 00
NORPRINT  HEX 0000
NOPAGE    HEX 00
STACKLVL  HEX 00
*
* PRGM
*
DEBUT     PHA
          JSR CHRGET
          PLA
          LDX #CMDADDR-CMDNAME
NEXTCMD   CMP CMDNAME-1,X
          BEQ CMDFOUND
          DEX
          BNE NEXTCMD
          JMP SYNTERR
CMDFOUND  DEX
          TXA
          ASL
          TAX
          LDA CMDADDR+1,X
          PHA
          LDA CMDADDR,X
          PHA
          TSX
          INX
          INX
          STX STACKLVL
          RTS            ;RETOUR PAR LA COMMANDE
CMDNAME   DFB CLEAR:,COLOR:,DRAW:,END:,FLASH:,GET:,GR:
          DFB HCOLOR:,HOME:,POS:,INPUT:,INVERSE:,NORMAL:
          DFB TRACE:,NOTRACE:,PLOT:
          DFB PRINT:,READ:,SCALE:,STEP:,TEXT:,VTAB:,WAIT:,XDRAW:,LOAD:
          DFB DEL:,IF:,ON:,AT:,HTAB:
CMDADDR   DA CLEAR-1,COLOR-1,DRAW-1,END-1,FLASH-1,GET-1,GR-1
          DA HCOLOR-1,HOME-1,POS-1,INPUT-1,INVERSE-1,NORMAL-1
          DA TRACE-1,NOTRACE-1,PLOT-1
          DA PRINT-1,READ-1,SCALE-1,STEP-1,TEXT-1,VTAB-1,WAIT-1,XDRAW-1
          DA LOAD-1,DEL-1,IF-1,ON-1,AT-1,HTAB-1
*
******************************
*                            *
* CETTE PARTIE EST UN 'BIOS' *
*                            *
* APPELE PAR TOUTES LES FONC-*
*                            *
* TIONS DE CRISTEL..         *
*                            *
******************************
*
          PUT BIOS.DTL2100+
*
* ROUTINE MSGOUT
*
MSGOUT    PLA
          STA TEMP
          PLA
          STA TEMP+1
          LDY #0
MSG2      INC TEMP
          BNE MSG3
          INC TEMP+1
MSG3      LDA (TEMP),Y
          BEQ MSG4
          ORA #$80
          JSR COUT
          JMP MSG2
MSG4      LDA TEMP+1
          PHA
          LDA TEMP
          PHA
          RTS
*
*
*
CSWNORM   LDA NORPRINT
          STA CSW
          LDA NORPRINT+1
          STA CSW+1
          RTS
*
CSWCRIS   LDA CSW
          STA NORPRINT
          LDA CSW+1
          STA NORPRINT+1
          STX CSW
          STY CSW+1
          RTS
*
*
*
INTERUPT  PHA
          TXA
          PHA
          TYA
          PHA
          JSR INT
          DO BUFSEND
          JSR SENDINT
          FIN
          PLA
          TAY
          PLA
          TAX
          PLA
          INC CHRGOT
          BNE INTRTS
          INC CHRGOT+1
INTRTS    RTS
INT       LDA FINBUF
          SEC
          SBC DEBUTBUF
          CMP #$FF
          BEQ INTRTS
          JSR CARECU
          BEQ INTRTS
          JSR RECOICAR
          LDY #0
          JSR SCROLL
          CMP #CR
          BNE RETINT
          LDX KSW
          CPX #NORINPUT
          BEQ RETINT
          JMP $3D0
RETINT    BIT $C081
          BIT $C081
          HEX 8D
FINBUF    HEX 00
          HEX D0
          BIT $C08A
          INC FINBUF
          JMP INT
*
*
*
          DO BUFSEND
SENDINT   LDA SENDBEG
          CMP SENDEND
          BNE SICONT
          LDA SENDBEG+1
          CMP SENDEND+1
          BNE SICONT
          RTS
SICONT    JSR SENDCLEA
          BNE SICONT2
          RTS
SICONT2   BIT $C080
          BIT $C080
          HEX AD
SENDBEG   HEX 00D1
          BIT $C08A
          LDY #$80
          JSR SCROLL
          JSR ENVOICAR
          INC SENDBEG
          BNE SI3
          INC SENDBEG+1
          LDA SENDBEG+1
          CMP #$E0
          BNE SI3
          LDA #$D1
          STA SENDBEG+1
SI3       RTS
          FIN
*
*
*
BFRON     LDX #5
LOOPON    LDA BFR1,X
          STA CHRGET,X
          DEX
          BPL LOOPON
RAZBUF    LDA #0
          STA FINBUF
          STA DEBUTBUF
          DO BUFSEND
RAZSEND   LDA #0
          STA SENDBEG
          STA SENDEND
          LDA #$D1
          STA SENDBEG+1
          STA SENDEND+1
          FIN
          RTS
BFR1      HEX 20
          DA INTERUPT
          HEX EAEAEA
          RTS
*
BFROFF    LDX #5
LOOPOFF   LDA BFR2,X
          STA CHRGET,X
          DEX
          BPL LOOPOFF
          BMI RAZBUF
BFR2      INC CHRGOT
          BNE WAIT2PT
          INC CHRGOT+1
*
*
*
WAIT2PT   LDY #0
          LDA (CHRGOT),Y
          BEQ END2PT
          CMP #':'
END2PT    RTS
*
*
*
SENDESC   STA TEMP
          LDA #ESC
          JSR SEND5
          LDA TEMP
          JMP SEND5
SENDIT    STA CARSEND
          JSR FILTSEND
          BCC SENDCHR
          RTS
SENDCHR   LDA XCOOR
          CMP #$FF
          BEQ SEND1
          JSR PORTEUSE
SEND1     LDA CARSEND
          CMP CAREP
          BEQ REP1
SEND6     LDA NBREP
          BEQ SEND2
          LDA NBREP
          CMP #1
          BEQ SEND4
REPEND    LDA #REP
          JSR SEND3
          LDA NBREP
          CLC
          ADC #$40
          JSR SEND3
          JMP SEND2
REP1      LDA CARSEND
          CMP #$20
          BMI SEND2
          LDA NBREP
          CMP #63
          BEQ REPEND
          INC NBREP
          RTS
SEND4     LDA CAREP
          JSR SEND3
SEND2     LDA #0
          STA NBREP
          LDA CARSEND
          STA CAREP
          DO BUFSEND
SEND3     CMP #$0C       ; EFFACE ECRAN...
          BNE SD
          JSR RAZSEND
          LDA #CAN
          JSR ENVOICAR
          LDA #FF
          JSR ENVOICAR
          JMP SD2
SD        BIT $C081
          BIT $C081
          HEX 8D
SENDEND   HEX 00D1
          BIT $C08A
          INC SENDEND
          BNE SD2BIS
          INC SENDEND+1
          LDA SENDEND+1
          CMP #$E0
          BNE SD2BIS
          LDA #$D1
          STA SENDEND+1
SD2BIS    LDA SENDEND
          CMP SENDBEG
          BNE SD2
          LDA SENDEND+1
          CMP SENDBEG+1
          BNE SD2
          JSR SICONT2
SD2       JMP INT
          ELSE
SEND3     LDY #$80
          JSR SCROLL
          JSR ENVOICAR
          JMP INT
          FIN
SEND5     STA CARSEND
          JSR SEND6
          LDA #0
          STA CAREP
          RTS
*
*
*
SCROLL    PHA
          LDX #0
SCLOOP2   LDA $401,Y
          STA $400,Y
          INY
          INX
          CPX #39
          BNE SCLOOP2
          PLA
          AND #$7F
          CMP #$20
          BMI CONTRTS
          ORA #$80
CONTRTS   STA $400,Y
          AND #$7F
          RTS
*
*
*
READIT    LDA FINBUF
          CMP DEBUTBUF
          BEQ READOLD
          BIT $C080
          BIT $C080
          HEX AD
DEBUTBUF  HEX 00
          HEX D0
          BIT $C08A
          INC DEBUTBUF
          RTS
READOLD   LDA #0
          STA TEMPOL
          STA TEMPOH
          LDA #-66
          STA SLOT16
WAITCHAR  JSR CARECU
          BNE RECEIVED
          DO BUFSEND
          JSR SENDINT
          FIN
          INC TEMPOL
          BNE WAITCHAR
          INC TEMPOH
          BNE WAITCHAR
          JSR PORTEUSE
          LDA SLOT16
          CMP #-15
          BNE WT2
          LDA #BELL
          JSR ENVOICAR
WT2       INC SLOT16
          BNE WAITCHAR
LOST      JSR END
          LDX STACKLVL
          TXS
          DO BUFSEND
          LDA KSW
          CMP #NORINPUT
          BEQ LOST2
          JMP $C600
          FIN
LOST2     LDA #500
          STA LINNUM
          LDA #/500
          STA LINNUM+1
          JSR GOTO
          JMP NEWSTT
RECEIVED  JSR RECOICAR
          JSR FILTRCVD
          BCS WAITCHAR
          LDY #0
          JSR SCROLL
          AND #$7F
          RTS
*
*
*
FILTRCVD  LDY #0
F1LOOP    CMP FILTER1,Y
          BEQ F1BAD
          LDX FILTER1,Y
          BEQ F1GOOD
          INY
          CPY #16
          BNE F1LOOP
F1GOOD    CLC
          RTS
F1BAD     SEC
          RTS
*
FILTSEND  LDY #0
F2LOOP    CMP FILTER2,Y
          BEQ F1BAD
          LDX FILTER2,Y
          BEQ F1GOOD
          INY
          CPY #16
          BNE F2LOOP
          BEQ F1GOOD
*
*
*
NEXTVAL   JSR CHKCOM
VALEUR    JSR $E6F8
          LDA $A1
          RTS
*
* PRISE DE MAIN PAR LE MINITEL...
*
          DO BUFSEND
ON        JSR VALEUR
          BEQ OFF
          LDX #SAPRINT2
          LDY #/SAPRINT2
          JSR CSWCRIS
          LDA #ON2
          STA KSW
          LDA #/ON2
          STA KSW+1
          LDA #1
          JSR STEPIN
          LDA #17
          JSR SENDIT
          JMP CLEAR
OFF       LDA #NORINPUT
          STA KSW
          LDA #/NORINPUT
          STA KSW+1
          LDA #PRINORM
          STA CSW
          LDA #/PRINORM
          STA CSW+1
          RTS
ONVAR     HEX 0000
ON2       STX ONVAR
          STY ONVAR+1
ONIN      LDA #17
          JSR SEND5
          JSR READIT
          CMP #SEP
          BNE ONRTS
ON3       JSR READIT
          CMP #SEP
          BEQ ON3
          AND #$0F
          TAX
          LDA ONTBL,X
          BMI ON4
ONRTS     LDX ONVAR
          LDY ONVAR+1
          ORA #$80
          RTS
ONTBL     HEX 200D2080201880082020202020202020
ON4       CPX #3
          BNE ON5
          JSR HOME
          JMP ONIN
ON5       JSR READIT
          AND #$1F
          JMP ONRTS
          ELSE
ON        RTS
          FIN
*
* WAIT: INITIALISATION ET ATTENTE
*
WAIT      JSR MSGOUT
          ASC 'ATTENTE'
          HEX 0D00
          JSR INITMDM
          JSR CONNECT
WAITOK    JSR BFRON
          LDA #$80
          STA MASK
          JSR HOME
          LDY #5
WTLOOP    LDA #$FF
          JSR WAITROM
          DEY
          BPL WTLOOP
*
*
*
CLEAR     LDA #0
          STA FINBUF
          STA DEBUTBUF
          JMP CLEARCV
*
*
*
HOME      JSR SENDMSG
          HEX 0C1F4041182012641B5D
          ASC 'JCA'
          HEX 1B5C1EFF
          RTS
*
*
*
HTAB      LDA #CR
          JSR SENDIT
          JSR VALEUR
          STA LEN2
          LDA #HT
          STA TEMP
          JMP PLOTLOOP
*
*
*
VTAB
*
*
POS       JSR VALEUR
          STA TEMP
          JSR WAIT2PT
          BEQ POS2
          JSR NEXTVAL
          JMP POSMNTL
POS2      LDA #1
POSMNTL   STA TEMP2
          LDA #US
          JSR SEND5
          LDA TEMP
          CLC
          ADC #$40
          JSR SEND5
          LDA TEMP2
          CLC
          ADC #$40
          JMP SEND5
*
*
*
DEL       JSR POS
          JSR WAIT2PT
          BNE DELBIS
DEL2      LDA #CAN
          JMP SEND5
DELBIS    LDA #0
          JSR HCOLOR2
          LDA #32
          STA TEMP
          JSR NEXTVAL
          JMP PLOT3
*
*
*
LOAD      JSR VALEUR
          AND #7
          STA NOPAGE
          ASL
          PHA
          JSR CHKCOM
          JSR BLOAD
          PLA
          TAX
          LDA DOSADD
          STA DOSTABL,X
          LDA DOSADD+1
          STA DOSTABL+1,X
          RTS
DOSTABL   DS 16
*
*
*
          DO PRODOS
BLOAD     LDX #BLOPRINT
          LDY #/BLOPRINT
          JSR CSWCRIS
          LDX #5
BLOOP     LDA BLODTBL-1,X
          STA BUFFER-1,X
          DEX
          BNE BLOOP
          LDA #5
          STA LEN
          JSR PRINTROM
          JSR CSWNORM
          JSR DOSCMD
          LDA DOSADD
          CLC
          ADC DOSLEN
          STA ENDPAGE+1
          LDA DOSADD+1
          ADC DOSLEN+1
          STA ENDPAGE+2
          LDA #$FF
ENDPAGE   STA ENDPAGE
          RTS
BLODTBL   HEX C2CCCFC1C4 ;'BLOAD'
BLOPRINT  STX TEMP2
          LDX LEN
          ORA #$80
          STA $200,X
          INC LEN
          LDX TEMP2
          RTS
          ELSE
BLOAD     JSR MSGOUT
          HEX 0D04
          ASC 'BLOAD'
          HEX 00
          LDX #1
          JSR BANKSEL
          LDA #14
          JSR PRINTROM
          LDX #2
          JMP BANKSEL
*
*
*
BANKSEL   TXA
          CLC
          ADC #$80
          STA RAM+1
          TAX
          LDY NOPAGE
          CPY #6
          BMI RAM2
          TXA
          CLC
          ADC #8
          STA RAM+1
RAM2      JSR RAM
RAM       LDA $C080
          RTS
          FIN
*
*
*
FLASH     LDA #'H'
          JMP SENDESC
*
*
*
INVERSE   LDA #']'
          JMP SENDESC
*
*
*
NORMAL    LDA #'I'
          JSR SENDESC
          LDA #'\'
          JMP SENDESC
*
*
*
COLOR     JSR VALEUR
          CLC
          ADC #$40
          JMP SENDESC
*
*
*
HCOLOR    JSR VALEUR
HCOLOR2   CLC
          ADC #$50
          JMP SENDESC
*
*
*
TEXT      LDA #SI
          JMP SENDIT
*
*
*
GR        LDA #SO
          JMP SENDIT
*
*
*
          DO BUFSEND
READ      LDY #19
READLOOP  LDA TABHORL,Y
          BMI READ2
          JSR READHORL
          STA BUFFER,Y
READNEXT  DEY
          BPL READLOOP
          LDA #JOUR
          JSR READHORL
          AND #$07
          ASL
          TAY
          LDA TABJOUR,Y
          STA BUFFER+9
          LDA TABJOUR+1,Y
          STA BUFFER+10
          LDA #20
          STA LEN2
          LDA #0
          STA TEMP
          JMP CARSEP2
READ2     AND #3
          TAX
          LDA TABCAR,X
          STA BUFFER,Y
          BPL READNEXT   ; JMP READNEXT
READHORL  STA HORL
          LDA HORL
          STA TEMP
          CPY #0
          BNE HORL2
          AND #$3
HORL2     CPY #12
          BNE HORL3
          AND #3
HORL3     CLC
          ADC #$30
          RTS
TABCAR    ASC '- /'
TABHORL   HEX 2524802322802120818181812827822A29822C2B
TABJOUR   ASC 'DILUMAMEJEVESA'
          ELSE
READ      RTS
          FIN
*
*
*
AT        JSR POS
          JSR CHKCOM
*
*
PRINT     LDA #$FF
          STA XCOOR
          JSR PORTEUSE
          JSR WAIT2PT
          BEQ PRINT3
          LDA #0
          STA TEMP
          LDX #SAVPRINT
          LDY #/SAVPRINT
          JSR CSWCRIS
          JSR PRINTROM
PRINERR   LDA #0
          STA XCOOR
          JMP CSWNORM
SAVPRINT  AND #%01111111
          PHA
          JSR PRINTIT
          PLA
          CMP #CR
          BEQ CROUT
          RTS
SAPRINT2  PHA
          JSR PRINORM
          PLA
          JMP SAVPRINT
CROUT     LDA #LF
PRINTIT   STX LEN
          STY TEMP2
          JSR SENDIT
          LDY TEMP2
          LDX LEN
          RTS
PRINT3    LDA #CR
          JSR SENDIT
          LDA #LF
          JMP SENDIT
*
*
*
SCALE     JSR VALEUR
          CLC
          ADC #76
          JMP SENDESC
*
*
*
END       JSR BFROFF
          JMP RACCROCH
*
*
*
TRACE     LDA #LINE
          JMP SENDESC
*
*
*
NOTRACE   LDA #NOLINE
          JMP SENDESC
*
*
*
PLOT      JSR VALEUR
PLOT2     STA TEMP
          JSR NEXTVAL
          STA LEN
PLOTIN    LDA LEN
PLOT3     STA LEN2
PLOTLOOP  DEC LEN2
          BMI PLOTRTS
          LDA TEMP
          JSR SENDIT
          JMP PLOTLOOP
PLOTRTS   RTS
*
*
*
XDRAW     LDA #0
          STA NOPAGE
          JSR BLOAD
          JMP LASTDRAW
*
*
*
INPUT     LDY #0
          LDA (CHRGOT),Y
          CMP #AT:
          BEQ INP2
          JSR POS
          JSR CHKCOM
          JMP INP3
INP2      JSR CHRGET
INP3      JSR VALEUR
          STA LEN
          JSR CHKCOM
INPUTIN   LDA CARINP
          STA TEMP
          JSR PLOTIN
          LDA #BS
          STA TEMP
          JSR PLOTIN
          LDA #CSRON
          JSR SEND5
          LDA #0
          STA LEN2
WAITCAR   JSR READIT
          STA TEMP
          CMP #SEP
          BEQ CARSEP
          LDA TEMP
          LDX LEN2
          STA $200,X
          LDX LEN2
          CPX LEN
          BEQ FULL
          LDA MASK
          BPL INPUT1
          LDA TEMP
INPUT1    JSR SEND3
          INC LEN2
          JMP WAITCAR
FULL      LDA #BELL
          JSR SEND5
          JMP WAITCAR
CARSEP    JSR READIT
          STA TEMP
          CMP #CORRECTI
          BEQ CORRECT
          LDA TEMP
          CMP #ANNULATI
          BNE CARSEP3
          LDA #CSROFF
          JSR SENDIT
          LDA #BS
          STA TEMP
          JSR PLOTLOOP
          JMP INPUTIN
CORRECT   LDA LEN2
          BEQ WAITCAR
          LDA #BS
          JSR SEND5
          LDA CARINP
          JSR SEND5
          LDA #BS
          JSR SEND5
          DEC LEN2
          JMP WAITCAR
CARSEP3   LDA #20
          JSR SEND5
CARSEP2   LDA #0
          STA LEN
          LDA TEMP
          AND #$F
          STA $FF
          LDA #$8D
          LDY LEN2
          STA BUFFER,Y
          JSR PTRGET
          JSR CHKSTR
          STA FORPNT
          STY FORPNT+1
          LDA #BUFFER
          LDY #/BUFFER
          LDX #$8D
          JSR MAK
          JMP SAVD
*
*
*
          DO BUFSEND
IF        JSR WAIT2PT
          BNE IFNORM
IFLOOP    LDA SENDBEG
          CMP SENDEND
          BNE CONTSEND
          LDA SENDEND+1
          CMP SENDBEG+1
          BNE CONTSEND
          RTS
CONTSEND  JSR SENDINT
          JSR INT
          JMP IFLOOP
IFNORM    LDA FINBUF
          ELSE
IF        LDA FINBUF
          FIN
          CMP DEBUTBUF
          BNE GET
          JSR CARECU
          BNE GET
          LDA #0
          STA BUFFER
          BEQ GET3
GET       JSR READIT
          STA BUFFER
          CMP #SEP
          BNE GET3
          JSR READIT
          AND #$F
          STA BUFFER
GET3      LDA #1
          STA LEN2
          LDA #0
          STA TEMP
          JMP CARSEP2
*
*
*
DRAW      LDA CSROFF
          JSR SENDIT
          JSR WAIT2PT
          BEQ LASTDRAW
          JSR VALEUR
          AND #7
          STA NOPAGE
          ASL
          TAX
          LDA DOSTABL+1,X
          STA TEMP+1
          LDA DOSTABL,X
SENDPAGE  STA TEMP
          JMP SMSG3
LASTDRAW  LDA DOSADD+1
          STA TEMP+1
          LDA DOSADD
          JMP SENDPAGE
SENDMSG   PLA
          STA TEMP
          PLA
          STA TEMP+1
          JSR SMSG2
          LDA TEMP+1
          PHA
          LDA TEMP
          PHA
          RTS
SMSG2     INC TEMP
          BNE SMSG3
          INC TEMP+1
          DO PRODOS
SMSG3     LDY #0
          LDA (TEMP),Y
          ELSE PRODOS
SMSG3     LDX #0
          JSR BANKSEL
          LDY #0
          LDA (TEMP),Y
          PHA
          LDX #2
          JSR BANKSEL
          PLA
          FIN PRODOS
          BMI SMSG4
          JSR SENDIT
          JMP SMSG2
SMSG4     RTS
*
*
*
STEP      JSR VALEUR
STEPIN    STA TEMP2
          LDA #':'
          JSR SENDESC
          LDA #'j'
          SEC
          SBC TEMP2
          JSR SENDIT
          LDA #'C'
          JMP SENDIT
          HEX 00
ENDCRIS   HEX 00
